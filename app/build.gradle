plugins {
    id 'com.android.application'
}

android {
    compileSdk 35
    buildToolsVersion = '35.0.0'

    defaultConfig {
        applicationId "com.onefishtwo.bbqtimer"
        versionCode 25
        versionName '4.2'
        minSdk 24
        targetSdk 35
        resourceConfigurations += ['en', 'de']

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // The following argument makes ANDROIDX_TEST_ORCHESTRATOR run
        // "adb shell pm clear <package>" before each test run, which resets the app's state.
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    viewBinding {
        enabled = false
    }

    buildTypes {
        debugProguard {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-test-rules.txt'
            testProguardFile 'proguard-test-rules.txt'
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.debug
            debuggable true
        }
        release {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.txt'
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_9
        targetCompatibility JavaVersion.VERSION_1_9
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }
    namespace 'com.onefishtwo.bbqtimer'
}

allprojects {
    tasks.withType(JavaCompile).tap {
        configureEach {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

dependencies {
    implementation 'androidx.activity:activity:1.9.3'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation "androidx.core:core:1.15.0"
    implementation 'androidx.media:media:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'

    testImplementation 'junit:junit:4.13.2'

    // See https://developer.android.com/training/testing/set-up-project for more.
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation 'androidx.test:runner:1.6.2'
    androidTestImplementation 'androidx.test:core:1.6.1'
    androidTestImplementation 'androidx.test:rules:1.6.1'
    // See https://github.com/android/android-test/issues/1042
    androidTestUtil 'androidx.test:orchestrator:1.5.1'

    // Workaround for failed task ':app:checkDebugDuplicateClasses' with androidx.activity:activity:1.7.2,
    // Duplicate class kotlin.collections.jdk8.CollectionsJDK8Kt found in jetified-kotlin-stdlib-1.8.0 and jetified-kotlin-stdlib-jdk8-1.7.21
    // See https://youtrack.jetbrains.com/issue/KT-55297/kotlin-stdlib-should-declare-constraints-on-kotlin-stdlib-jdk8-and-kotlin-stdlib-jdk7
    implementation(platform('org.jetbrains.kotlin:kotlin-bom:1.9.22'))
}
