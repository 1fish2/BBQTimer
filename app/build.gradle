plugins {
    id 'com.android.application'
}

android {
    compileSdkVersion 31
    buildToolsVersion '31.0.0'

    defaultConfig {
        applicationId "com.onefishtwo.bbqtimer"
        versionCode 16
        versionName '2.6'
        minSdkVersion 21
        targetSdkVersion 28
        resConfigs "en", "de"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // The following argument makes ANDROIDX_TEST_ORCHESTRATOR run
        // "adb shell pm clear <package>" before each test run, which resets the app's state.
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.txt'
            minifyEnabled true
            shrinkResources true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        // Due to a OS/libs/tools bug, this orchestrator fails on Android 12. Commenting it out gets
        // further but the app will have leftover state and its Activity might not start and set up.
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }
}

allprojects {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.media:media:1.4.3'

    testImplementation 'junit:junit:4.13.2'

    // See https://developer.android.com/training/testing/set-up-project for more.
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:core:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestUtil 'androidx.test:orchestrator:1.4.0'
}
