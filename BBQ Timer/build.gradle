apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.1'

    signingConfigs {
        releaseSigning {
            // The dummy passwords complete the signingConfigs so the other tasks can work without
            // asking for passwords when not needed.
            storeFile file(System.getenv("HOME") + "/.android/1fish2_keystore.jks")
            storePassword "notTheRealPassword"
            keyAlias "BBQTimer"
            keyPassword "notTheRealPassword"
        }
    }

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 24
        versionCode 14
        versionName "2.4"
        resConfigs "en","de"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.txt'
            //noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.releaseSigning
            zipAlignEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

task askForPasswords << {
    // Construct a String because readPassword() returns char[] and using that fails silently.
    def storePw = new String(System.console().readPassword("\nKeystore password: "))
    def keyPw   = new String(System.console().readPassword("\nKey password: "))

    android.signingConfigs.releaseSigning.storePassword = storePw
    android.signingConfigs.releaseSigning.keyPassword = keyPw
}

tasks.whenTaskAdded { theTask ->
    if (theTask.name.equals("packageRelease")) {
        theTask.dependsOn "askForPasswords"
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:24.2.0'
    compile 'com.android.support:support-compat:24.2.0'
    compile 'com.android.support:design:24.2.0'
}
